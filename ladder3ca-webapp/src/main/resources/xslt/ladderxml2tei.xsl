<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:xs="http://www.w3.org/2001/XMLSchema"
    xmlns="http://www.tei-c.org/ns/1.0"
    exclude-result-prefixes="xs"
    version="2.0">
    
    <xsl:output method="xml" indent="no"/>
    <xsl:param name="title">Data from Ladder 3 CA</xsl:param>
    <xsl:param name="query"></xsl:param>
    <xsl:param name="currentDate" select="current-dateTime()"></xsl:param>
    
    <xsl:template match="/">
        <TEI>
            <teiHeader>
                <fileDesc>
                    <titleStmt>
                        <title>
                            <xsl:value-of select="$title"/>
                        </title>
                        <author>Ladder 3 CA Web Application</author>
                    </titleStmt>
                    <publicationStmt>
                        <p>Data generated by Ladder 3 CA Web Application. Data Generated on <xsl:value-of select="$currentDate"/> by using the query:</p>
                        <p><xsl:value-of select="$query"/></p>
                    </publicationStmt>
                    <sourceDesc>
                        <p>Generated Document from Ladder 3 CA Web Application.</p>
                    </sourceDesc>
                </fileDesc>
            </teiHeader>
            <text>
                <front>
                    <div>
                        <head>Modifiers</head>
                        <p>These modifiers are available in the database at the time of data download.</p>
                        <xsl:apply-templates select="/CorpusData/modifierList"/>
                    </div>
                    <div>
                        <head>Subacts</head>
                        <p>These subacts are available in the database at the time of data download.</p>
                        <xsl:apply-templates select="/CorpusData/subactList"/>
                    </div>
                </front>
                <body>
                    <div>
                        <head>
                            <xsl:value-of select="$title"/>
                        </head>
                        <p>These texts are generated from the Ladder 3 CA Database. Each &lt;div&gt; is a text in the corpus.</p>
                        <xsl:apply-templates select="/CorpusData/records"/>
                    </div>
                </body>
            </text>
        </TEI>
    </xsl:template>
    
    <xsl:template match="modifierList/modifierList">
        <div>
            <head>
                <xsl:attribute name="n" select="id"/>
                <xsl:attribute name="xml:id">
                    <xsl:text>mod_</xsl:text>
                    <xsl:value-of select="id"/>
                </xsl:attribute>
                <xsl:value-of select="modifierCode"/>
            </head>
            <p>
                <xsl:value-of select="desc"/>
            </p>
        </div>
    </xsl:template>
    
    <xsl:template match="subactList/subactList">
        <div>
            <head>
                <xsl:attribute name="n" select="id"/>
                <xsl:attribute name="xml:id">
                    <xsl:text>sub_</xsl:text>
                    <xsl:value-of select="id"/>
                </xsl:attribute>
                <xsl:value-of select="subactName"/>
            </head>
            <xsl:if test="parentSubact/subactName">
                <p>This is a subact of <ref>
                    <xsl:attribute name="target">
                        <xsl:text>#sub_</xsl:text>
                        <xsl:value-of select="parentSubactId"/>
                    </xsl:attribute>
                    <xsl:value-of select="parentSubact/subactName"/>
                </ref>.</p>
            </xsl:if>
            <p><xsl:value-of select="desc"/></p>
        </div>
    </xsl:template>
    
    <xsl:template match="records/records">
        <div>
            <xsl:attribute name="n" select="textMetadata/id"/>
            <head><xsl:apply-templates select="textMetadata/id"/></head>
            <div>
                <head>Data and Metadata</head>
                <xsl:apply-templates select="textMetadata"/>
            </div>
            <div>
                <head>Annotations</head>
                <xsl:call-template name="createTokenTable"></xsl:call-template>
            </div>
        </div>
    </xsl:template>
    
    <xsl:template name="createTokenTable">
        <xsl:variable name="record" select="."/>
        <table>
            <row role="heading">
                <cell>Position</cell>
                <cell>Token</cell>
                <cell>Modifier Annotations</cell>
                <cell>Subact Annotations</cell>
            </row>
            <xsl:for-each select="textMetadata/tokens/tokens">
                <xsl:variable name="databasePosition" select="position() -  1"/>
                <row>
                    <cell>
                        <xsl:value-of select="position()"/>
                    </cell>
                    <cell>
                        <xsl:value-of select="."/>
                    </cell>
                    <cell>
                        <xsl:for-each select="$record/modifierAnnotations/modifierAnnotations[startTn=$databasePosition]">
                            <xsl:variable name="modifierId" select="modifierId"/>
                            <xsl:variable name="modifier" select="/CorpusData/modifierList/modifierList[id=$modifierId]"/>
                            <ref>
                                <xsl:attribute name="target">
                                    <xsl:text>#mod_</xsl:text>
                                    <xsl:value-of select="$modifierId"/>
                                </xsl:attribute>
                                <xsl:value-of select="$modifier/modifierCode"/>
                            </ref>
                        </xsl:for-each>
                    </cell>
                    <cell>
                        <xsl:for-each select="$record/subactAnnotations/subactAnnotations[startTn=$databasePosition]">
                            <xsl:variable name="subactId" select="subactId"/>
                            <xsl:variable name="subact" select="/CorpusData/subactList/subactList[id=$subactId]"/>
                            <ref>
                                <xsl:attribute name="target">
                                    <xsl:text>#sub_</xsl:text>
                                    <xsl:value-of select="$subactId"/>
                                </xsl:attribute>
                                <xsl:value-of select="$subact/subactName"/>
                            </ref>
                        </xsl:for-each>
                    </cell>
                </row>
            </xsl:for-each>
        </table>
    </xsl:template>
    
    <xsl:template match="creationTask">
        <ref>
            <xsl:attribute name="target">
                <xsl:text>creationTask:</xsl:text>
                <xsl:value-of select="id"/>
            </xsl:attribute>
           <xsl:apply-templates select="taskName"/>
        </ref>
        <note>
           <p><xsl:apply-templates select="desc"/></p>
        </note>
    </xsl:template>
    
    <xsl:template match="textMetadata">
        <p><xsl:apply-templates select="textdata"/></p>
        <table>
            <row>
                <cell role="heading">ID</cell>
                <cell><xsl:value-of select="id"/></cell>
            </row>
            <row>
                <cell role="heading">Alt ID</cell>
                <cell><xsl:value-of select="altId"/></cell>
            </row>
            <row>
                <cell role="heading">Language Code</cell>
                <cell><xsl:value-of select="languageCode"/></cell>
            </row>
            <row>
                <cell role="heading">Creation Task</cell>
                <cell>
                    <xsl:apply-templates select="creationTask"/>
                </cell>
            </row>
            <row>
                <cell role="heading">Last Modified Timestamp</cell>
                <cell><xsl:value-of select="lastModified"/></cell>
            </row>
            <row>
                <cell role="heading">Speaker Gender</cell>
                <cell><xsl:value-of select="gender"/></cell>
            </row>
            <row>
                <cell role="heading">Speaker Age when Recording</cell>
                <cell><xsl:value-of select="ageAtCreation"/></cell>
            </row>
            <row>
                <cell role="heading">Speaker L1</cell>
                <cell><xsl:value-of select="l1Language"/></cell>
            </row>
            <row>
                <cell role="heading">Speaker L2s</cell>
                <cell><xsl:value-of select="l2Languages"/></cell>
            </row>
            <row>
                <cell role="heading">Speaker Location</cell>
                <cell><xsl:value-of select="location"/></cell>
            </row>
        </table>
    </xsl:template>
    
    
</xsl:stylesheet>